buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
        classpath 'me.tatarka:gradle-retrolambda:2.0.+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'retrolambda'

apply from: 'local.gradle'

// Manifest version information!
def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionBuild = 0 // bump for dogfood builds, public betas, etc.

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 20
    buildToolsVersion '20.0.0'

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 20

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://repo.commonsware.com.s3.amazonaws.com'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    provided 'com.squareup.dagger:dagger-compiler:1.2.1'

    compile 'com.jakewharton.timber:timber:2.2.+'
    compile 'com.jakewharton:butterknife:5.1.+'
    compile 'com.google.code.gson:gson:2.2.+'
    compile 'com.squareup.okhttp:okhttp:2.0.+'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.+'
    compile 'com.squareup.retrofit:retrofit:1.6.+'
    compile 'com.squareup.picasso:picasso:2.3.+'
    compile 'com.squareup.dagger:dagger:1.2.+'
    compile 'com.netflix.rxjava:rxjava-android:0.19.+'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.+'
    compile 'com.sothree.slidinguppanel:library:+'

    debugCompile 'com.jakewharton.madge:madge:1.1.+'
    debugCompile 'com.jakewharton.scalpel:scalpel:1.1.+'
    debugCompile 'com.squareup.retrofit:retrofit-mock:1.6.+'
}
